#!/usr/bin/perl -w
#########################################################
#                                                       #
#                                                       #
#       SNMP Interface Traffic Check Plugin             #
#                      for                              #
#                Icinga / Nagios                        #
#                                                       #
#        Version 1.0.2 (October 05, 2022)               #
#                by Bob McLoughlin                      #
#         E-mail: rmcloughlin@outlook.com               #
#                                                       #
#                                                       #
# Based on:                                             #
# check_snmp_iftraffic                                  #
#       Version .77 (March 27, 2018)                    #
# ( by Greg Frater                                      #
#       E-mail: gregATfraterfactory.com )               #
#                                                       #
# Which was further based on:                           #
# check_iftraffic.pl                                    #
#       Version     (June 14, 2005)                     #
# ( by Markus Werner                                    #
#       E-mail: mw+nagios@wobcom.de )                   #
#                                                       #
# Version History                                       #
#   - Refer to CHANGELOG.md                             #
#                                                       #
#       Like the original, this plugin is distributed   #
# under the GNU GPL license. You may re-destribute only #
# according to the terms of the GNU GPL.                #
#                                                       #
#########################################################
#########################################################
#                                                       #
# DEPENDS On                                            #
# Net-SNMP                                              #
#                                                       #
#########################################################

# Set Perl environ
use strict;
use lib qw ( /usr/local/nagios/libexec /usr/lib/nagios/plugins /usr/lib64/nagios/plugins );
use utils qw( %ERRORS &print_revision );
use Data::Dumper;
use SNMP;
use Getopt::Long;
&Getopt::Long::config('bundling');

use vars qw (
  $PROGNAME $VERSION $opt_h $opt_authproto $opt_authpass $opt_bits $opt_community
  $opt_warn_usage $opt_crit_usage $opt_iface_speed $opt_iface_speedOut $opt_force 
  $opt_contextname $opt_port $opt_units $opt_iface_descr $opt_host_address $opt_index_list $opt_debug 
  $opt_username $opt_snmpver $opt_max_value $opt_privproto $opt_disabletotals $opt_disableperf 
  $opt_privpass $opt_authkey $opt_privkey $opt_thirtytwo $opt_version $opt_timeout $opt_retries
);

# config
$PROGNAME    = $0;
$VERSION     = '1.0.2';

# Set script parameters
my $in_bytes = 0;
my $out_bytes = 0;
my $in_bytes32 = 0;
my $out_bytes32 = 0;
my $in_ave = 0;
my $pin_ave = 0;
my $in_tot = 0;
my $out_ave = 0;
my $pout_ave = 0;
my $out_tot = 0;
my $pdInSpeed = 1;
my $pdOutSpeed = 1;
my $pdInUsage = 1;
my $pdOutUsage = 1;
my $pdInBwdth = 1;
my $pdOutBwdth = 1;
my $pdInAbs = 1;
my $pdOutAbs = 1;
my ( $error, $iface_number, $ifspeed64, $ifspeed32, $inbytes64, $inbytes32, $outbytes64, $outbytes32,
     $max_bytes, $response, $session, $max_speed, $max_speedOut, $timeout);
my @snmpoids;
my $if_status     = '4';
my $label         = '';
my $output        = '';
my $state         = 'UNKNOWN';
my $suffix        = 'B';

# SNMP OIDs for Traffic
my $snmpIfOperStatus 	= '1.3.6.1.2.1.2.2.1.8';	# Operational state of interface (i.e. 1-up, 2-down, etc.)
my $snmpIfInOctets32  	= '1.3.6.1.2.1.2.2.1.10';	# Total Octets entering interface (in) - 32 bit counter
my $snmpIfOutOctets32 	= '1.3.6.1.2.1.2.2.1.16';	# Total Octets leaving interface (out) - 32 bit counter
my $snmpIfInOctets  	= '1.3.6.1.2.1.31.1.1.1.6';	# Total Octets entering interface (in) - 64 bit counter
my $snmpIfOutOctets 	= '1.3.6.1.2.1.31.1.1.1.10';	# Total Octets leaving interface (out) - 64 bit counter
my $snmpIfDescr     	= '1.3.6.1.2.1.2.2.1.2';	# Textual string describing interface
my $snmpIfSpeed32     	= '1.3.6.1.2.1.2.2.1.5'; 	# bits per second
my $snmpIfSpeed    	= '1.3.6.1.2.1.31.1.1.1.15'; 	# Mbits per second
my $snmpIPAdEntIfIndex 	= '1.3.6.1.2.1.4.20.1.2'; 	# Index of interface using hosts IP addr

# Path to  tmp files
my $TRAFFIC_FILE = "/usr/local/nagios/libexec/traffic/";

# Create tmp file location if it does not exist
if ( ! -d $TRAFFIC_FILE ) {
	mkdir $TRAFFIC_FILE;
}

# changes sos 20090717 UNKNOWN must be 3
# Set status codes
my %STATUS_CODE =
  ( 'UNKNOWN' => '3', 'OK' => '0', 'WARNING' => '1', 'CRITICAL' => '2' );

# init options
my $opt_h              = undef;
my $opt_version        = undef;
my $opt_bits           = undef; 
my $opt_community      = 'public';
my $opt_port 	       = 161;
my $opt_crit_usage     = 98;
my $opt_debug          = 0;
my $opt_force          = undef;
my $opt_snmpver        = 2;
my $opt_thirtytwo      = undef;
my $opt_warn_usage     = 85;
my $opt_username       = undef;
my $opt_authpass       = undef;
my $opt_authproto      = undef;
my $opt_authkey        = undef;
my $opt_privpass       = undef;
my $opt_privproto      = undef;
my $opt_privkey        = undef;
my $opt_contextname    = undef;
my $opt_disabletotals  = undef;
my $opt_iface_speed    = undef;
my $opt_iface_speedOut = undef;
my $opt_index_list     = undef;
my $opt_max_value      = undef; #added 20050614 by mw
my $opt_host_address   = undef;
my $opt_iface_descr    = undef;
my $opt_units          = 'm';
my $opt_disableperf    = undef;
my $opt_timeout        = 10;
my $opt_retries        = 5;

my $status = GetOptions(
        "a|authproto=s"                => \$opt_authproto,
        "A|authpass=s"                 => \$opt_authpass,
        "b|bandwidth|I|inBandwidth=i"  => \$opt_iface_speed,
        "B|bits"                       => \$opt_bits,
        "c|critical=s"                 => \$opt_crit_usage,
        "C|community=s"                => \$opt_community,
        "d|debug=i"                    => \$opt_debug,
        "f|force"                      => \$opt_force, #added 20130429 by gjf
        "h|help"                       => \$opt_h,
        "H|hostname=s"                 => \$opt_host_address,
        "i|interface=s"                => \$opt_iface_descr,
        "L|list"                       => \$opt_index_list,
        "M|max=i"                      => \$opt_max_value, #added 20050614 by mw
        "n|contextname=s"              => \$opt_contextname,
        "O|outBandwidth=i"             => \$opt_iface_speedOut,
        "p|port=i"                     => \$opt_port,
        "q|disableperf=s"              => \$opt_disableperf,
        "r|retries=i"                  => \$opt_retries,
        "t|disabletotals"              => \$opt_disabletotals,
        "T|timeout=i"                  => \$opt_timeout,
        "u|units=s"                    => \$opt_units,
        "user|username=s"              => \$opt_username,
        "v|snmpver=s"                  => \$opt_snmpver,
        "V|version"                    => \$opt_version,
        "w|warning=s"                  => \$opt_warn_usage,
        "x|privproto=s"                => \$opt_privproto,
        "X|privpass=s"                 => \$opt_privpass,
        "y|authkey=s"                  => \$opt_authkey,
        "z|privkey=s"                  => \$opt_privkey,
        "32|32bit"                     => \$opt_thirtytwo #added 20101104 by gjf
);

# set alarm in case we hang
$SIG{ALRM} = sub {
         print "CRITICAL - Timeout after " . $opt_timeout * ( $opt_retries + 1 ) . " seconds\n";
         exit($ERRORS{'CRITICAL'});
};
alarm( $opt_timeout * ( $opt_retries + 1 ) );

# Print help if no options given
if ( $opt_h ) {
	print_usage();
	exit $STATUS_CODE{'OK'};
}

if($opt_version) {
  print_version();
  exit $STATUS_CODE{'OK'};
}

debugout ("DEBUG ENABLED at level: $opt_debug", "1");
debugout ("INTERFACE DESCR: $opt_iface_descr", "2"), if ( defined($opt_iface_descr) );

# Changed 20091214 gj
# Check for missing options
if ( !$opt_host_address )  {
	print  "\nMissing host address!\n\n";
	stop(print_usage(),"OK");
}
if ( !$opt_index_list ) {
        if ( ( $opt_iface_speed ) and ( !$opt_units ) ) {
                print "\nMissing units!\n\n";
                stop(print_usage(),"OK");
        } elsif ( ( $opt_units ) and ( ( !$opt_iface_speed ) and  ( !$opt_iface_speedOut ) ) ) {
                print "\nMissing (-I or -O) interface maximum speed!\n\n";
                stop(print_usage(),"OK");
        } elsif ( ( $opt_iface_speedOut ) and ( !$opt_units ) ) {
                print "\nMissing units (-u) for Out maximum speed!\n\n";
                stop(print_usage(),"OK");
        } elsif ( ( $opt_max_value ) and ( !$opt_units ) ) {
                print "\nMissing units (-u) for maximum counter!\n\n";
                stop(print_usage(),"OK");
        } elsif ( ( $opt_snmpver == 3 ) and ( $opt_authpass ) and ( !$opt_username ) ) {
                print "\nMissing version 3 username!\n\n";
                stop(print_usage(),"OK");
        } elsif ( ( $opt_snmpver == 3 ) and ( $opt_privpass ) and ( !$opt_authpass ) ) {
                print "\nMissing version 3 auth password!\n\n";
                stop(print_usage(),"OK");
        }
}

sub print_version {
  print_revision($PROGNAME, $VERSION);
}

# Set default units if undefined
if ( !defined($opt_units) ){
	#bits
	$opt_units = "b";
}

# Switch output from Bytes to bits if changed on command line
if ($opt_bits) {
	$suffix = "b";
}

# Convert timeout to microseconds
$timeout = ($opt_timeout * 1000000);

# Added to preserve iface native values 20220919 by rm
if ( $opt_iface_speed ) { $max_speed = $opt_iface_speed; }
if ( $opt_iface_speedOut ) { $max_speedOut = $opt_iface_speedOut; } else { $max_speedOut = $opt_iface_speed; }

if ( $opt_iface_speed ) {
	#change 20050414 by mw
	# Added iface_speedOut 20100202 by gj
	# Convert interface speed to Bytes
	debugout("UNIT: $opt_units","3");
	$opt_iface_speed = bits2bytes( $opt_iface_speed, $opt_units );
	if ( $opt_iface_speedOut ) {
		$opt_iface_speedOut = bits2bytes( $opt_iface_speedOut, $opt_units );
	}
}

if ( !$opt_max_value ) {

	# If no -M Parameter was set, set it to 64Bit Overflow
	$max_bytes = 18446744073709600000;    # the value is (2^64)

	if ( defined($opt_thirtytwo) ){
		# If using 32bit counters set to 32bit overflow
		$max_bytes = 4294967296;    # the value is (2^32)
	}
} else {
	# Max value specified, used given value, convert to Bytes
        $max_bytes = unit2bytes( $opt_max_value, $opt_units );
}

debugout ("BYTE COUNTER max_value: $max_bytes", "2");

# disable perfdata if requested
if ($opt_disableperf) {
        if ($opt_disableperf =~ m/a/) { $pdInSpeed = 0; }
        if ($opt_disableperf =~ m/b/) { $pdOutSpeed = 0; }
        if ($opt_disableperf =~ m/c/) { $pdInUsage = 0; }
        if ($opt_disableperf =~ m/d/) { $pdOutUsage = 0; }
        if ($opt_disableperf =~ m/e/) { $pdInBwdth = 0; }
        if ($opt_disableperf =~ m/f/) { $pdOutBwdth = 0; }
        if ($opt_disableperf =~ m/g/) { $pdInAbs = 0; }
        if ($opt_disableperf =~ m/h/) { $pdOutAbs = 0; }
}

# Check snmp version, set snmp parameters
# changed 20220919 by rm

if (!$opt_authpass) { $opt_authpass = $opt_authkey; }
if (!$opt_privpass) { $opt_privpass = $opt_privkey; }

#base snmp arguments
my %snmpargs = (DestHost      => $opt_host_address,
                Version       => $opt_snmpver,
                RemotePort    => $opt_port,
                Timeout       => $timeout,
                Retries       => $opt_retries,
);

#snmp v1 and v2c specific arguments
my %snmpv12 = ( Community  => $opt_community, );

#snmpv3 arguments
#base
my %snmpv3 = ( SecName     => $opt_username, );

if ( $opt_username and $opt_authpass and $opt_privpass ) {
        #authPriv
        my %snmpv3ap = ( SecLevel    => 'authPriv',
                         AuthPass    => $opt_authpass,
                         AuthProto   => $opt_authproto,
                         PrivPass    => $opt_privpass,
                         PrivProto   => $opt_privproto, );
        %snmpv3 = ( %snmpv3, %snmpv3ap );
} elsif ( $opt_username and $opt_authpass ) {
        #authNoPriv
        my %snmpv3anp = ( SecLevel    => 'authNoPriv',
                          AuthPass    => $opt_authpass,
                          AuthProto   => $opt_authproto, );
        %snmpv3 = ( %snmpv3, %snmpv3anp );
} else {
        #noAuthNoPriv
        my %snmpv3nanp = ( SecLevel    => 'noAuthNoPriv', );
        %snmpv3 = ( %snmpv3, %snmpv3nanp );
}

if ( $opt_contextname ) { %snmpv3 = ( %snmpv3, Context    => $opt_contextname, ); }

my %snmpv3key = ( AuthLocalizedKey     => $opt_authkey,
                  PrivLocalizedKey     => $opt_privkey, );

if ( $opt_snmpver le 2 ) {
        %snmpargs = ( %snmpargs, %snmpv12 );
} elsif ( $opt_snmpver eq 3 ) {
        if ( $opt_authkey or $opt_privkey ) {
                %snmpargs = ( %snmpargs, %snmpv3, %snmpv3key );
        } else {
                %snmpargs = ( %snmpargs, %snmpv3 );
        }
} else {
	$state = 'UNKNOWN';
	stop( "$state: It appears you are missing one or more parameters necessary for v$opt_snmpver to function.\n",$state );
}
$session = new SNMP::Session( %snmpargs );

debugout("SNMP:\n\tver: $opt_snmpver\n\tcommunity: $opt_community\n\tport: $opt_port", "3");
if ( $opt_username ) {
	debugout("\n\tuser: $opt_username", "3");
}
if ( $opt_authpass ) {
	debugout("\n\tauth password: $opt_authpass\n\tauth protocol: $opt_authproto", "3");
}
if ( $opt_privpass ) {
	debugout("\n\tpriv password: $opt_privpass\n\tpriv protocol: $opt_privproto", "3");
}

# convert to IP
if ( $opt_host_address =~ /\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}/ ) {
} else {
        $opt_host_address = get_ip ( $opt_host_address );
}

# Interface Index was not specified 
if ( !$opt_iface_descr ) {
        $opt_iface_descr = fetch_Ip2IfIndex( $session, $opt_host_address );
}

# Added 20091209 gj
# Detect if a string description was given or a numeric interface index number 
if ( $opt_iface_descr =~ /[^0123456789]+/ ) {
	$iface_number = fetch_ifdescr( $session, $opt_iface_descr );
	debugout("INTERFACE from text string","1"); 
} else {
	$iface_number = $opt_iface_descr;
}

debugout("USING INT#: $iface_number", "2");
debugout("OID's:","2");
debugout("\tIfOperStatus: $snmpIfOperStatus.$iface_number","2");
debugout("\tIfSpeed: $snmpIfSpeed.$iface_number","2");
debugout("\tIfSpeed32: $snmpIfSpeed32.$iface_number","2");
debugout("\tInOctets: $snmpIfInOctets.$iface_number","2");
debugout("\tInOctets32: $snmpIfInOctets32.$iface_number","2");
debugout("\tOutOctets: $snmpIfOutOctets.$iface_number","2");
debugout("\tOutOctets32: $snmpIfOutOctets32.$iface_number","2");

# Get SNMP values, flag errors
debugout("RESULTS:","3");
# Changed 20220919 rm
# Simplified
my $res_status     = get_results($snmpIfOperStatus, $iface_number, "Operational Status", "not defined");
$ifspeed64         = get_results($snmpIfSpeed, $iface_number, "Interface Speed (64bit)", "not defined or bad value");
$ifspeed32         = get_results($snmpIfSpeed32, $iface_number, "Interface Speed (32bit)", "not defined");
$inbytes64         = get_results($snmpIfInOctets, $iface_number, "In Bytes (64bit)", "not defined");
$inbytes32         = get_results($snmpIfInOctets32, $iface_number, "In Bytes (32bit)", "not defined");
$outbytes64        = get_results($snmpIfOutOctets, $iface_number, "Out Bytes (64bit)", "not defined");
$outbytes32        = get_results($snmpIfOutOctets32, $iface_number, "Out Bytes (32bit)", "not defined");

if ( defined $res_status ) { $if_status = $res_status; }

# Check interface status, quit check if interface status is error or not '1'.
if ( defined $res_status ){
	if ( $if_status =~ /^DNE -|error|noSuchObject|noSuchInstance/i ) {
		debugout("if_status is not a number\n","4");
		$state = 'UNKNOWN';
		stop("$state: Improper interface status, status received: " . $if_status . "\n",$state);
	} elsif ( $if_status == 1 ) {
		# No action necessary, continue on!
	} elsif ($if_status == 2 ) {
		# Quit, interface is down
		$state = 'CRITICAL';
		stop("$state: Interface is \'down\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} elsif ($if_status == 3 ) {
		# Quit, interface is testing
		$state = 'CRITICAL';
		stop("$state: Interface is reporting \'testing\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} elsif ($if_status == 4 ) {
		# Quit, interface is unknown
		$state = 'CRITICAL';
		stop("$state: Interface is reporting \'unknown\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} elsif ($if_status == 5 ) {
		# Quit, interface is dormant
		$state = 'CRITICAL';
		stop("$state: Interface is reporting \'dormant\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} elsif ($if_status == 6 ) {
		# Quit, interface is notPresent
		$state = 'CRITICAL';
		stop("$state: Interface is reporting \'notPresent\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} elsif ($if_status == 7 ) {
		# Quit, interface is lowerLayerDown
		$state = 'CRITICAL';
		stop("$state: Interface is reporting \'lowerLayerDown\' (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	} else {
		# Interface is in an unknown state
		$state = 'UNKNOWN';
		stop("$state: Interface is reporting unknown status (interface: " . $iface_number . " status: " . $if_status . ")\n",$state);
	}
} else {
	$state = 'UNKNOWN';
	stop("$state: Improper interface status, status received: " . $if_status . "\n",$state);
}

# Determine which values to use
if ( !defined($opt_force) and !defined($opt_thirtytwo) ){
	#default - use 64 then 32 bit values 
	debugout("USING 64 THEN 32 bit VALUES:","4");

	# Speed test
	if ( !$opt_iface_speed ) {
		debugout("\t(Interface speed not provided on command line, determine from host)","4");
		if ( $ifspeed64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed64 < 1 ){
			debugout("\t\tBad 64bit speed value: $ifspeed64","3");
			if ( $ifspeed32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed32 < 1 or $ifspeed32 > 4294967295 ){
				debugout("\t\tBad 32bit speed value: $ifspeed32","3");
				$state = 'UNKNOWN';
				stop("$state: No usable interface speed available 64: $ifspeed64 32: $ifspeed32\n",$state);
			} else {
				debugout("\tUsing 32bit speed value: $ifspeed32","3");
				$opt_iface_speed = bits2bytes( $ifspeed32, "b" );
			}
		} else {
			debugout("\tUsing 64bit speed value: $ifspeed64","3");
			# Convert Mbits to bits, bits to Bytes
			$opt_iface_speed = $ifspeed64 * 1000 * 1000;
			$opt_iface_speed = bits2bytes( $opt_iface_speed, "b" );
		}
	}

	# InBytes test
	if ( $inbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 64bit in bytes value: $inbytes64","3");
		if ( $inbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
			debugout("\t\tBad 32bit in bytes value: $inbytes32","3");
			$state = 'UNKNOWN';
			stop("$state: No in bytes available 64: $inbytes64 32: $inbytes32\n",$state);
		} else {
			debugout("\tUsing 32bit in bytes value: $inbytes32","3");
			$in_bytes = $inbytes32;
		}
	} else {
		debugout("\tUsing 64bit in bytes value: $inbytes64","3");
		$in_bytes = $inbytes64;
	}	

	# OutBytes test
	if ( $outbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 64bit out bytes value: $outbytes64","3");
		if ( $outbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
			debugout("\t\tBad 32bit out bytes value: $outbytes32","3");
			$state = 'UNKNOWN';
			stop("$state: No out bytes available 64: $outbytes64 32: $outbytes32\n",$state);
		} else {
			debugout("\tUsing 32bit out bytes value: $outbytes32","3");
			$out_bytes = $outbytes32;
		}
	} else {
		debugout("\tUsing 64bit out bytes value: $outbytes64","3");
		$out_bytes = $outbytes64;
	}	

} elsif ( !defined($opt_force) and defined($opt_thirtytwo) ){
	debugout("USING 32 THEN 64 bit VALUES:","4");

	# Speed test
	if ( !$opt_iface_speed ) {
		debugout("\t(Interface speed not provided on command line, determine from host)","4");
		if ( $ifspeed32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed32 < 1 or $ifspeed32 >= 4294967295 ){
			debugout("\t\tBad 32bit speed value: $ifspeed32","3");
			if ( $ifspeed64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed64 < 1 ){
				debugout("\t\tBad 64bit speed value: $ifspeed64","3");
				$state = 'UNKNOWN';
				stop("$state: No usable interface speed available 64: $ifspeed64 32: $ifspeed32\n",$state);
			} else {
				debugout("\tUsing 64bit speed value: $ifspeed64","3");
				# Convert Mbits to bits, bits to Bytes
				$opt_iface_speed = $ifspeed64 * 1000 * 1000;
				$opt_iface_speed = bits2bytes( $opt_iface_speed, "b" );
			}
		} else {
			debugout("\tUsing 32bit speed value: $ifspeed32","3");
			$opt_iface_speed = bits2bytes( $ifspeed32, "b" );
		}
	}

	# InBytes test
	if ( $inbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 32bit in bytes value: $inbytes32","3");
		if ( $inbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
			debugout("\t\tBad 64bit in bytes value: $inbytes64","3");
			$state = 'UNKNOWN';
			stop("$state: No in bytes available 64: $inbytes64 32: $inbytes32\n",$state);
		} else {
			debugout("\tUsing 64bit in bytes value: $inbytes64","3");
			$in_bytes = $inbytes64;
		}
	} else {
		debugout("\tUsing 32bit in bytes value: $inbytes32","3");
		$in_bytes = $inbytes32;
	}	

	# OutBytes test
	if ( $outbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 32bit out bytes value: $outbytes32","3");
		if ( $outbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
			debugout("\t\tBad 64bit out bytes value: $outbytes64","3");
			$state = 'UNKNOWN';
			stop("$state: No out bytes available 64: $outbytes64 32: $outbytes32\n",$state);
		} else {
			debugout("\tUsing 64bit out bytes value: $outbytes64","3");
			$out_bytes = $outbytes64;
		}
	} else {
		debugout("\tUsing 32bit out bytes value: $outbytes32","3");
		$out_bytes = $outbytes32;
	}	

} elsif ( defined($opt_force) and defined($opt_thirtytwo) ){
	debugout("USING 32 bit VALUES ONLY:","4");

	# Speed test
	if ( !$opt_iface_speed ) {
		debugout("\t(Interface speed not provided on command line, determine from host)","4");
		if ( $ifspeed32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed32 < 1 or $ifspeed32 > 4294967295 ){
			debugout("\t\tBad 32bit speed value: $ifspeed32","3");
			$state = 'UNKNOWN';
			stop("$state: No usable interface speed available: $ifspeed32\n",$state);
		} else {
			debugout("\tUsing 32bit speed value: $ifspeed32","3");
			$opt_iface_speed = bits2bytes( $ifspeed32, "b" );
		}
	}

	# InBytes test
	if ( $inbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 32bit in bytes value: $inbytes32","3");
		$state = 'UNKNOWN';
		stop("$state: No in bytes available: $inbytes32\n",$state);
	} else {
		debugout("\tUsing 32bit in bytes value: $inbytes32","3");
		$in_bytes = $inbytes32;
	}	

	# OutBytes test
	if ( $outbytes32 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 32bit out bytes value: $outbytes32","3");
		$state = 'UNKNOWN';
		stop("$state: No out bytes available: $outbytes32\n",$state);
	} else {
		debugout("\tUsing 32bit out bytes value: $outbytes32","3");
		$out_bytes = $outbytes32;
	}

} elsif ( defined($opt_force) and !defined($opt_thirtytwo) ){
	debugout("USING 64 bit ONLY:","4");

	# Speed test
	if ( !$opt_iface_speed ) {
		debugout("\t(Interface speed not provided on command line, determine from host)","4");
		if ( $ifspeed64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i or $ifspeed64 < 1 ){
			debugout("\t\tBad 64bit speed value: $ifspeed64","3");
			$state = 'UNKNOWN';
			stop("$state: No usable interface speed available: $ifspeed64\n",$state);
		} else {
			debugout("\tUsing 64bit speed value: $ifspeed64","3");
			# Convert Mbits to bits, bits to Bytes
			$opt_iface_speed = $ifspeed64 * 1000 * 1000;
			$opt_iface_speed = bits2bytes( $opt_iface_speed, "b" );
		}
	}

	# InBytes test
	if ( $inbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 64bit in bytes value: $inbytes64","3");
		$state = 'UNKNOWN';
		stop("$state: No in bytes available: $inbytes64\n",$state);
	} else {
		debugout("\tUsing 64bit in bytes value: $inbytes64","3");
		$in_bytes = $inbytes64;
	}	

	# OutBytes test
	if ( $outbytes64 =~ /^DNE -|error|noSuchObject|noSuchInstance/i ){
		debugout("\t\tBad 64bit out bytes value: $outbytes64","3");
		$state = 'UNKNOWN';
		stop("$state: No out bytes available: $outbytes64\n",$state);
	} else {
		debugout("\tUsing 64bit out bytes value: $outbytes64","3");
		$out_bytes = $outbytes64;
	}

}

# Added 20100201 gj
# Check if Out max speed was provided, use same IF speed for both if not
if (!$opt_iface_speedOut) {
	$opt_iface_speedOut = $opt_iface_speed;
}

debugout ("UNITS: $opt_units", "2");
debugout ("Interface speed calculated as: $opt_iface_speed $opt_units", "1");
debugout ("Interface speed OUT: $opt_iface_speedOut $opt_units", "4");

my $row;
my $last_check_time = time - 1;
my $last_in_bytes   = $in_bytes;
my $last_out_bytes  = $out_bytes;

if (
	open( FILE,
		"<" . $TRAFFIC_FILE . $opt_host_address . "_if-" . $iface_number )
  )
{
	while ( $row = <FILE> ) {

		( $last_check_time, $last_in_bytes, $last_out_bytes ) =
		  split( ":", $row );

		### by sos 17.07.2009 check for last_bytes
		if ( ! $last_in_bytes  ) { $last_in_bytes=$in_bytes;  }
		if ( ! $last_out_bytes ) { $last_out_bytes=$out_bytes; }

		if ($last_in_bytes !~ m/\d/) { $last_in_bytes=$in_bytes; }
		if ($last_out_bytes !~ m/\d/) { $last_out_bytes=$out_bytes; }
	}
	close(FILE);
}

my $update_time = time;
debugout ("CURRENT timestamp: $update_time", "1");
debugout ("FILE PATH: $TRAFFIC_FILE" . $opt_host_address . "_if-" . $iface_number, "1");
debugout("READ FROM FILE:\n\tlast time: $last_check_time\n\tlast in bytes: $last_in_bytes\n\tlast out bytes: $last_out_bytes","4");

open( FILE, ">" . $TRAFFIC_FILE . $opt_host_address . "_if-" . $iface_number )
  or die "Can't open $TRAFFIC_FILE for writing: $!";

printf FILE ( "%s:%.0ld:%.0ld\n", $update_time, $in_bytes, $out_bytes );
close(FILE);

debugout("WRITTEN TO FILE:\n\ttime: $update_time\n\tin_bytes: $in_bytes\n\tout bytes: $out_bytes","4");

#added 20050614 by mw
#Check for and correct counter overflow (if possible).
#See function counter_overflow.
$in_bytes  = counter_overflow( $in_bytes,  $last_in_bytes,  $max_bytes );
$out_bytes = counter_overflow( $out_bytes, $last_out_bytes, $max_bytes );

# Calculate traffic since last check (RX\TX) 
my $in_traffic = sprintf( "%.2lf",
	( $in_bytes - $last_in_bytes ) / ( time - $last_check_time ) );
my $out_traffic = sprintf( "%.2lf",
	( $out_bytes - $last_out_bytes ) / ( time - $last_check_time ) );

debugout("in_traffic: $in_traffic\nout_traffic: $out_traffic","1");
# sos 20090717 changed because rrdtool needs bytes
my $in_bytes_abs  = $in_bytes;
my $out_bytes_abs = $out_bytes;

debugout("iface_speed: $opt_iface_speed $opt_units\nin_traffic: $in_traffic\nout_traffic: $out_traffic", "1");

# Calculate usage percentages
my $in_ave_pct  = sprintf( "%.2f", ( 1.0 * $in_traffic * 100 ) / $opt_iface_speed );
my $out_ave_pct = sprintf( "%.2f", ( 1.0 * $out_traffic * 100 ) / $opt_iface_speedOut );

# Don't scale perf data or change it to bits from bytes
$pin_ave = $in_traffic;
$pout_ave = $out_traffic;

if ($opt_bits) {
	# Convert output from Bytes to bits
	$label = "bits";
	$in_tot = $in_bytes * 8;
	$out_tot = $out_bytes * 8;
	$in_ave = $in_traffic * 8;
	$out_ave = $out_traffic * 8;	
} else {
	$label = "Bytes";
	$in_tot = $in_bytes;
	$out_tot = $out_bytes;
	$in_ave = $in_traffic;
	$out_ave = $out_traffic;
}

debugout("OUTPUT:\n\tlabel: $label", "4");
debugout("\tin_tot: $in_tot\n\tout_tot: $out_tot\n\tin_ave: $in_ave\n\tout_ave: $out_ave", "4");
debugout("\tin_ave_pct: $in_ave_pct\n\tout_ave_pct: $out_ave_pct\n\tin_bytes_abs: $in_bytes_abs\n\tout_bytes_abs: $out_bytes_abs", "4");

# Scale ave and tot for output
$in_tot = unit2scale($in_tot, $opt_units);
$out_tot = unit2scale($out_tot, $opt_units);
$in_ave = unit2scale($in_ave, $opt_units);
$out_ave = unit2scale($out_ave, $opt_units);

# Convert from Bytes/bits to megaBytes/bits
$in_bytes  = sprintf( "%.2f", $in_bytes / (1024 * 1000) );
$out_bytes = sprintf( "%.2f", $out_bytes / (1024 * 1000) );

# Changed 20091209 gj, updated 20130424 gj
# Check/Set state of service check
if ( ( $in_ave_pct < $opt_warn_usage ) and ( $out_ave_pct < $opt_warn_usage ) ) {
	$state = 'OK';
	$output =
	"$state - Average IN: "
	  . $in_ave . $suffix . "ps (" . $in_ave_pct . "%), " 
	  . "Average OUT: " . $out_ave . $suffix . "ps (" . $out_ave_pct . "%)";
        # changed 20220921
        # allowed this output to be disabled
	if ( !$opt_disabletotals ) { $output .= " Total RX: $in_tot" . "$label, Total TX: $out_tot" . "$label"; }
} elsif ( $in_ave_pct > $opt_crit_usage ) {
	$state = 'CRITICAL';
	$output = "$state - IN bandwidth ($in_ave_pct%) too high";
} elsif ( $out_ave_pct > $opt_crit_usage ) {
	$state = 'CRITICAL';
	$output = "$state - OUT bandwidth ($out_ave_pct%) too high";
} elsif ( $in_ave_pct > $opt_warn_usage ) {
	$state = 'WARNING';
	$output = "$state - IN bandwidth ($in_ave_pct%) too high";
} elsif ( $out_ave_pct > $opt_warn_usage ) {
	$state = 'WARNING';
	$output = "$state - OUT bandwidth ($out_ave_pct%) too high";
}


# Changed 20091214 gj - commas should have been semicolons
# Changed 20220919 rm - each perf output needs four semicolons.  Also made 
# perfdata output custom depending on needs

$output .= "|"; 
if ($pdInSpeed) { 
        $output .= "inSpeed=$in_ave";
        if ( $suffix =~ /[b]/ ) { 
                $output .= lc $suffix;
        } else {
                $output .= $suffix;
        }
        $output .= "ps;;;0;$max_speed ";
} 
if ($pdOutSpeed) {
        $output .= "outSpeed=$out_ave";
        if ( $suffix =~ /[b]/ ) {
                $output .= lc $suffix;
        } else {
                $output .= $suffix;
        }
        $output .= "ps;;;0;$max_speedOut ";
} 
if ($pdInUsage) { $output .= "inUsage=$in_ave_pct%;$opt_warn_usage;$opt_crit_usage;; "; }
if ($pdOutUsage) { $output .= "outUsage=$out_ave_pct%;$opt_warn_usage;$opt_crit_usage;; "; }
if ($pdInBwdth) { $output .= "inBandwidth=" . $pin_ave . "B;;;; "; }
if ($pdOutBwdth) { $output .= "outBandwidth=" . $pout_ave . "B;;;; "; }
if ($pdInAbs) {  $output .= "inAbsolut=$in_bytes_abs" . "c;;;; "; }
if ($pdOutAbs) { $output .= "outAbsolut=$out_bytes_abs" . "c;;;;"; }
debugout ("",1);
stop($output, $state);


# end of internal timeout eval group
#alarm 0;
#};

sub fetch_Ip2IfIndex {
	my $state;
	my @IPInterfaces;
        my @AllInterfaces;

	my $snmpkey;
	my $key;

        my $n = 0;

	my ( $session, $host ) = @_;

        @IPInterfaces = $session->bulkwalk(0, 1000, new SNMP::VarList([$snmpIPAdEntIfIndex]));
        @AllInterfaces = $session->bulkwalk(0, 1000, new SNMP::VarList([$snmpIfDescr]));
	# Quit if results not found
        if ($session->{ErrorNum}) {
		$state = 'CRITICAL';
                stop ("$state: Interface index not detected ERROR: $session->{ErrorStr}",$state);
        } 

        # Changed 20220919 rm
        # Formatting changes and adding ability to list all interfaces not just IP based ones
	if ( $opt_index_list ){
		print ("\n[Interfaces with IPs found:]\n");
		print ("  IP Addr\t\t\tIndex\n");
		print ("----------------------------------------\n");
	}
	# Check each returned value
        while ( $n < scalar( @{ $IPInterfaces[0] }) ) {
		if ( $opt_index_list ){
			printf ("%-25s", "$IPInterfaces[0][$n][1]");
                        print "\t  $IPInterfaces[0][$n][2]\n";
		}

		# Check for ip address match in returned index results
		if ( $IPInterfaces[0][$n][1] =~ /$host$/ ) {
			$snmpkey = $IPInterfaces[0][$n][2];
		}
                $n++;
	}
        $n = 0;
        if ( $opt_index_list ){
                print ("\n[All Interfaces:]\n");
                print ("    Interface\t\t\tIndex\n");
                print ("----------------------------------------\n");
                while ( $n < scalar( @{ $AllInterfaces[0] }) ) {
                        printf ("%-25s", "$AllInterfaces[0][$n][2]");
                        print "\t  $AllInterfaces[0][$n][1]\n";
                $n++;
                }
                $state = 'OK';
                stop("$state: Listed current interfaces.",$state);
        }

	unless ( defined $snmpkey ) {
		$state = 'CRITICAL';
		stop("$state: Could not find index matching $host",$state);
	}
	return $snmpkey;
}

sub fetch_ifdescr {
	my $state;
	my $resp;
        my @response;
	my $snmpkey;
	my $answer;
	my $key;

        my $n = 0;

	my ( $session, $ifdescr ) = @_;

        # Changed 20220919 rm
        # Converted from Net::SNMP to SNMP
        @response = $session->bulkwalk(0, 1000, new SNMP::VarList([$snmpIfDescr]));
        # Quit if results not found
        if ($session->{ErrorNum}) {
                $state = 'CRITICAL';
                exit $STATUS_CODE{$state};
        }

        while ($n < scalar( @{ $response[0] } ) ) {
                if ($response[0][$n][2] =~ /^$ifdescr$/) {
                        $snmpkey = $response[0][$n][1];
                        debugout("\t$ifdescr = $snmpkey","2");
                }

            $n++;
        }

	unless ( defined $snmpkey ) {
		$state = 'CRITICAL';
		printf "$state: Could not match $ifdescr \n";
		exit $STATUS_CODE{$state};
	}
	return $snmpkey;
}

#added 20050416 by mw
#Converts an input value to value in bits
sub bits2bytes {
	return unit2bytes(@_) / 8;
}

#added 20050416 by mw
#Converts an input value to value in bytes
sub unit2bytes {
	my ( $value, $unit ) = @_;

	if ( $unit eq "g" ) {
		return $value * 1000 * 1000 * 1000;
	}
	elsif ( $unit eq "m" ) {
		return $value * 1000 * 1000;
	}
	elsif ( $unit eq "k" ) {
		return $value * 1000;
	}
	elsif ( $unit eq "b" ) {
		return $value * 1;
	}
	else {
		print "You have to supply a supported unit (g,m,k,b)\n";
		exit $STATUS_CODE{'UNKNOWN'};
	}
}

sub unit2scale {
	# Scale output, expecting Bits\Bytes input
	my ($val, $opt_units) = @_;
	my $prefix = "";


        # changed 20220919
        if ( $opt_units eq "k" ) {
		$val = sprintf( "%.2f", $val / 1000 );
		$prefix = "K";
	}
        if ( $opt_units eq "m" ) {
		$val = sprintf( "%.2f", $val / 1000 / 1000 );
		$prefix = "M";
	}
        if ( $opt_units eq "g" ) {
		$val = sprintf( "%.2f", $val / 1000 / 1000 / 1000 );
		$prefix = "G";
	}
	return $val . $prefix;
}

# Convert from Bytes/bits to megaBytes/bits
# added 20050414 by mw
# This function detects if an overflow occurs. If so, it returns
# a computed value for $bytes.
# If there is no counter overflow it simply returns the original value of $bytes.
# IF there is a Counter reboot wrap, just use previous output.
sub counter_overflow {
	my ( $bytes, $last_bytes, $max_bytes ) = @_;

	$bytes += $max_bytes if ( $bytes < $last_bytes );
	$bytes = $last_bytes  if ( $bytes < $last_bytes );
	return $bytes;
}

# Added 20100202 by gj
# Print results and exit script
sub stop {
	my $result = shift;
	my $exit_code = shift;
	print $result . "\n";
	exit ( $STATUS_CODE{$exit_code} );
}

# Added 20100405 by gj
# Lookup hosts ip address
sub get_ip {
	use Net::DNS;

	my ( $host_name ) = @_;

	my $res = Net::DNS::Resolver->new;
	my $query = $res->search($host_name);

	if ($query) {
		foreach my $rr ($query->answer) {
			next unless $rr->type eq "A";
			return $rr->address;
		}
	} else {
		
		stop("Error: IP address not resolved","UNKNOWN");
	}
}

# Added 20220919 rm
# Grab SNMP results
sub get_results {
        my ($oid, $inum, $smsg, $emsg) = @_;

        my $result = $session->get(new SNMP::Varbind( [$oid . "." . $inum ] ));
        if ($session->{ErrorNum}) {
                $result = "DNE - $session->{ErrorStr}";
                debugout("\t$smsg $emsg!: DNE - $session->{ErrorStr}","2");
        } else {
                debugout("\t$smsg: $result","3");
        }
        return $result;
}

sub debugout {

	my ( $text, $level ) = @_;

	if ( $level <= $opt_debug) {
		print "$text\n";
	}
}

sub print_usage {
	print <<EOU;
    Usage: check_snmp_iftraffic -H host [ -C community_string ] [ -i if_index|if_descr ] [ -b if_max_speed_in | -I if_max_speed_in ] [ -O if_max_speed_out ] [ -u ] [ -B ] [ --32bit ] [ -f ] [ -L ] [ -M ] [ -w warn ] [ -c crit ] [ -v 1|2|3 ] [ --username <username> ] [ --contextname <contextname> ] [ --authpassword <authpassword> ] [ --authprotocol MD5|SHA ] [ --privpassword <privpassword> ] [ --privprotocol DES|3DES|AES|AES192|AES256|AES256C ] [ --authkey <authkey> ] [ --privkey <privkey> ] [ -q abcdefgh ] [ -t ] [ -T <seconds> ] [ -r <integer> ]

    Example 1: check_snmp_iftraffic -H host1 -C sneaky
    Example 2: check_snmp_iftraffic -H host1 -C sneaky -i "Intel Pro" -r -B  
    Example 3: check_snmp_iftraffic -H host1 -C sneaky -i 5
    Example 4: check_snmp_iftraffic -H host1 -C sneaky -i 5 -B -b 100 -u m --32bit
    Example 5: check_snmp_iftraffic -H host1 -C sneaky -i 5 -B -I 20 -O 5 -u m 
    Example 6: check_snmp_iftraffic -H host1 -C sneaky -A 192.168.1.1 -B -b 100 -u m 
    Example 7: check_snmp_iftraffic -H host1 -C sneaky -A 192.168.1.1 --force --32bit 
    Example 8: check_snmp_iftraffic -H host1 --username admin --authpassword sneaky  
    Example 9: check_snmp_iftraffic -H host1 --username admin --authpassword sneaky123 --privpassword reallysneaky --privprotocol 3DES  

    Options:


    -b, --bandwidth INTEGER
    -I, --inBandwidth INTEGER
        Interface maximum speed in kilo/mega/giga/bits per second. Applied to 
	both IN and OUT if out (-O) max speed is not provided. Requires -u.
    -O, --outBandwidth INTEGER
        Interface maximum speed in kilo/mega/giga/bits per second. Applied to
	OUT traffic.  Uses the same units value given for -b. Requires -u. 
    -i, --interface STRING
        Interface Name
    -M, --max INTEGER
	Max Counter Value (in bits) of net devices in giga/mega/kilo/bits. Requires -u.
    -u, --units STRING
        g=gigabits/s,m=megabits/s,k=kilobits/s,b=bits/s. Required if -b, -I, -M,
	or -O are used.
    -C, --community STRING 
        SNMP Community.
    -H, --host STRING or IPADDRESS
        Check interface on the indicated host.
    -c, --critical INTEGER
        % of bandwidth usage necessary to result in critical status (default: 98%)
    -w, --warning INTEGER
        % of bandwidth usage necessary to result in warning status (default: 85%)
    --32bit FLAG
	Set to use 32 bit counters instead of 64 bit (default: 64 bit).
    -B, --bits FLAG
	Display results in bits per second b/s (default: Bytes/s)
    -f, --force FLAG
	Set to force either 64 bit or 32 bit only checking (32 bit requires --32bit flag).
    -d, --debug INTEGER
	Output some debug info, not supported inside of Nagios but may be useful
	from the command line.  Levels 1-4 can be specified, 4 being the most information.
    -h, --help
	Displays this help text
    -L, --list FLAG (on/off)
	Tell plugin to list available interfaces. This is not supported inside 
	of Nagios but may be useful from the command line.
    -v, --Version STRING
	Set SNMP version (defaults to 2).  Version 2 or 3 required for 64 bit counters.
    -S, --seclevel=STRING
        Security level for SNMPv3 connection  (noAuthNoPriv, authNoPriv, authPriv)
    -A, --authpass=STRING
        SNMPv3 authentication password
    -a, --authproto=STRING
        SNMPv3 authentication protocol
    -r, --retries
        Sets how many times to retry SNMP (default: 5)
    -t, --disabletotals
        Disables RX and TX Total output
    -T, --timeout=INTEGER
        Sets timeout value for SNMP (default: 30s)
    -q, --disableperf=STRING
        Disable specific perfdata 
        (a - InSpeed, b - OutSpeed, c - InUsage, d - OutUsage, e - InBwdth, f - OutBwdth, g - InAbsolute, f - OutAbsolute)
    -y, --authkey=STRING
        SNMPv3 authentication key
    -X, --privpass=STRING
        SNMPv3 privacy password
    -x, --privproto=STRING
        SNMPv3 privacy protocol
    -z, --privkey=STRING
        SNMPv3 privacy key
EOU

}
